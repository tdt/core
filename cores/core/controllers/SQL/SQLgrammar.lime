%class SQLgrammar
%start filterresult

statement = filterresult
                {$$ = $1;}
            | expression
                {$$ = $1;}
            | '(' filterresult/stat ')' asopt name/tablealias
                {$$ = putFilterAfterIfExists($stat, new TableAliasFilter($tablealias));}
            .

filterresult = selectfromstatement {$$ = $1;/*TODO*/}
             | unionstatement {$$ = $1;/*TODO*/}
             .

unionstatement = expression/expr1 UNION expression/expr2 
                { throw new Exception("Union is not supported yet..."); /*TODO*/}
               .

selectfromstatement = SELECT uniquefilter/distinct selecttablelist/selecttables FROM fromtableexpression/fromtable wherefilter/where groupfilter/groupby orderbyfilter/orderby limitfilter/limit
                        {// 1) fromtables
                         // 2) wherefilter (if defined)
                         // 3) groupbyfilter (if defined)
                         // 4) selecttables (if not '*')
                         // 5) distinctfilter (if defined)
                        
                        $totalfilter=$fromtable;
                        $totalfilter=putFilterAfterIfExists($totalfilter, $where);
                        $totalfilter=putFilterAfterIfExists($totalfilter, $groupby);
                        $totalfilter=putFilterAfterIfExists($totalfilter, $selecttables);
                        $totalfilter=putFilterAfterIfExists($totalfilter, $distinct);
                        $totalfilter=putFilterAfterIfExists($totalfilter, $orderby);
                        $totalfilter=putFilterAfterIfExists($totalfilter, $limit);
                        
                        $$=$totalfilter;}
                    .

uniquefilter = DISTINCT {$$ = new DistinctFilter();}
             | {$$ = null;}
             . 

wherefilter = WHERE expression/expr {$$ = new FilterByExpressionFilter($expr);}
            | {$$ = null;}
            .

groupfilter = GROUP BY identifierlist/groupFields havingfilter/having 
                   {$$ = putFilterAfterIfExists(new DataGrouper($groupFields), $having);}
           |  {$$ = null;}
           .

havingfilter = HAVING expression/expr {$$ = new FilterByExpressionFilter($expr);}
             | {$$ = null;}
             .

limitfilter = LIMIT constant/limit 
               {$$ = new LimitFilter(null, 0, $limit);}
            | LIMIT constant/limit OFFSET constant/offset 
               {$$ = new LimitFilter(null, $offset, $limit);}
            |  {$$ = null;}
            .

orderbyfilter = ORDER BY orderbyidentifierlist/columns
                    { $$ = new SortFieldsFilter($columns);}
              | {$$ = null;}
              .

orderbyidentifierlist = orderbyfield/f
                        { $$ = array($f); }
                      | orderbyfield/f ',' orderbyidentifierlist/flist 
                        { array_unshift($flist, $f); $$ = $flist; }
                      .

orderbyfield = name/fieldname ASC
                    { $$ = new SortFieldsFilterColumn(new Identifier($fieldname), SortFieldsFilterColumn::$SORTORDER_ASCENDING); }
             | name/fieldname DESC
                    { $$ = new SortFieldsFilterColumn(new Identifier($fieldname), SortFieldsFilterColumn::$SORTORDER_DESCENDING); }
             | name/fieldname
                    { $$ = new SortFieldsFilterColumn(new Identifier($fieldname));}
             .

fromtableexpression = expression {$$=$1;}
                    | expression/stat asopt name/tablealias
                       {$$ = putFilterAfterIfExists($stat, new TableAliasFilter($tablealias));}
                    .

selecttablelist =  listoftables {$$ = new ColumnSelectionFilter($1);}
                .

listoftables = expression/expr 
                {$$ = array(new ColumnSelectionFilterColumn($expr, null));}
          | expression/expr asopt allowedalias/alias 
                {$$ = array(new ColumnSelectionFilterColumn($expr, $alias));}
          | expression/expr ',' listoftables/arr 
                {array_unshift($arr, new ColumnSelectionFilterColumn($expr, null));$$ = $arr;}
          | expression/expr asopt allowedalias/alias ',' listoftables/arr 
                {array_unshift($arr, new ColumnSelectionFilterColumn($expr, $alias));$$ = $arr;}
          .
          
constantlist = constant {$$ = array(new Constant($1));}
             | constant ',' constantlist/arr {array_unshift($arr, new Constant($1));$$ = $arr;}
             .

asopt = AS | .

expression = expressionA  {$$ = $1;}
           | expressionA/a BETWEEN expressionD/b AND expressionD/c 
                 {$$ = CombinedFilterGenerators::makeBetweenFilter($a,$b,$c);}
           | expressionA IN '(' constantlist/constantlist ')' 
                 {$$ = putFilterAfterIfExists($1, new CheckInFunction($constantlist));}
           | expressionA/a INNER JOIN expressionA/b ON expressionA/c
                 {$$ = null;/*TODO*/}
           | expressionA/a LEFT JOIN expressionA/b ON expressionA/c 
                 {$$ = null;/*TODO*/}
           | expressionA/a RIGHT JOIN expressionA/b ON expressionA/c 
                 {$$ = null;/*TODO*/}
           | expressionA/a FULL JOIN expressionA/b ON expressionA/c 
                 {$$ = null;/*TODO*/}
           .

expressionA = expressionAB/a '|' expressionA/b 
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_CONCAT, $a, $b);}
            | expressionAB {$$ = $1;}
            .
            
expressionAB = expressionB/a OR expressionAB/b 
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_OR, $a, $b);}
            | expressionB {$$ = $1;}
            .
            
expressionB = expressionC/a AND expressionB/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_AND, $a, $b);}
            | expressionC {$$ = $1;}
            .
            
expressionC = expressionD/a '=' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_EQUAL, $a, $b);}
            | expressionD/a '<' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_SMALLER_THAN, $a, $b);}
            | expressionD/a '>' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_LARGER_THAN, $a, $b);}
            | expressionD/a '<' '=' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_SMALLER_OR_EQUAL_THAN, $a, $b);}
            | expressionD/a '>' '=' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_LARGER_OR_EQUAL_THAN, $a, $b);}
            | expressionD/a '<' '>' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_NOTEQUAL, $a, $b);}
            | expressionD/a '<' ALL expressionD/b
                {$$ = CombinedFilterGenerators::makeSmallerThanAllOrAnyFilter($a,$b);}
            | expressionD/a '<' '=' ALL expressionD/b
                {$$ = CombinedFilterGenerators::makeSmallerThanAllOrAnyFilter($a,$b,false);}
            | expressionD/a '<' ANY expressionD/b
                {$$ = CombinedFilterGenerators::makeSmallerThanAllOrAnyFilter($a,$b, true, false);}
            | expressionD/a '<' '=' ANY expressionD/b
                {$$ = CombinedFilterGenerators::makeSmallerThanAllOrAnyFilter($a,$b, false, false);}
            | expressionD/a '>' ALL expressionD/b
                {$$ = CombinedFilterGenerators::makeLargerThanAllOrAnyFilter($a,$b);}
            | expressionD/a '>' '=' ALL expressionD/b
                {$$ = CombinedFilterGenerators::makeLargerThanAllOrAnyFilter($a,$b, false);}
            | expressionD/a '>' ANY expressionD/b
                {$$ = CombinedFilterGenerators::makeLargerThanAllOrAnyFilter($a,$b, true, false);}
            | expressionD/a '>' '=' ANY expressionD/b
                {$$ = CombinedFilterGenerators::makeLargerThanAllOrAnyFilter($a,$b, false, false);}
            | expressionD/a '!' '=' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_COMPARE_NOTEQUAL, $a, $b);}
            | expressionD/expr LIKE likeexpression/likeexpr
                 {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_MATCH_REGEX, $expr, new Constant(convertRegexFromSQLToUniversal($likeexpr)));}
            | expressionD
                {$$ = $1;}
            .

expressionD = expressionE/a '+' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_PLUS, $a, $b);}
            | expressionE/a '-' expressionD/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_MINUS, $a, $b);}
            | expressionE
                {$$ = $1;}
            .

expressionE = expressionF/a '*' expressionE/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_MULTIPLY, $a, $b);}
            | expressionF/a '/' expressionE/b
                {$$ = new BinaryFunction(BinaryFunction::$FUNCTION_BINARY_DIVIDE, $a, $b);}
            | expressionF 
                {$$ = $1;}
            .

expressionF = endexpression {$$ = $1;}
            .

endexpression = identifier 
                {$$ = new Identifier($1);}
            | constant
                {$$ = new Constant($1);}
            | identifier/functid '(' ')'
                {$$ = getNularyFilterForSQLFunction($functid);}
            | EXTRACT '(' extractconstant/constant FROM statement/arg ')'
                {$$ = getExtractFunction($arg, getExtractConstant($constant));}
            | DATE_ADD '(' statement/date ',' INTERVAL statement/interval extractconstant/constant ')'
                {$$ = getDateAddFunction(true, $date, $interval, getExtractConstant($constant));}
            | DATE_SUB '(' statement/date ',' INTERVAL statement/interval extractconstant/constant ')'
                {$$ = getDateAddFunction(false, $date, $interval, getExtractConstant($constant));}
            | identifier/functid '(' statement/arg1 ')' 
                {$$ = getUnaryFilterForSQLFunction($functid, $arg1);}
            | identifier/functid '(' statement/arg1 ',' statement/arg2 ')'
                {$$ = getBinaryFunctionForSQLFunction($functid, $arg1, $arg2);}
            | identifier/functid '(' statement/arg1 ',' statement/arg2 ',' statement/arg3 ')'
                {$$ =getTertaryFunctionForSQLFunction($functid, $arg1, $arg2, $arg3);}
            | identifier/functid '(' statement/arg1 ',' statement/arg2 ',' statement/arg3 ',' statement/arg4 ')'
                {$$ =getQuadernaryFunctionForSQLFunction($functid, $arg1, $arg2, $arg3, $arg4);}
            | '(' statement/stat ')'
                {$$ = $stat;}
            .

likeexpression = constant {$$ = $1;}
               .

identifierlist = identifier {$$ = array(new Identifier($1));}
               | identifier ',' identifierlist/arr {array_unshift($arr, new Identifier($1));$$=$arr;}
               .

allowedalias = name {$$=$1;}
             .

identifier = name {$$=$1;} 
           | '*' {$$="*";} 
           .

extractconstant = 
    SECOND {$$=DateTimeExtractConstants::$EXTRACT_SECOND;} |
    MINUTE {$$=DateTimeExtractConstants::$EXTRACT_MINUTE;} |
    HOUR {$$=DateTimeExtractConstants::$EXTRACT_HOUR;} |
    DAY {$$=DateTimeExtractConstants::$EXTRACT_DAY;} |
    WEEK {$$=DateTimeExtractConstants::$EXTRACT_WEEK;} |
    MONTH {$$=DateTimeExtractConstants::$EXTRACT_MONTH;} |
    YEAR {$$=DateTimeExtractConstants::$EXTRACT_YEAR;} |
    MINUTE_SECOND {$$=DateTimeExtractConstants::$EXTRACT_MINUTE_SECOND;} |
    HOUR_SECOND {$$=DateTimeExtractConstants::$EXTRACT_HOUR_SECOND;} |
    HOUR_MINUTE {$$=DateTimeExtractConstants::$EXTRACT_HOUR_MINUTE;} |
    DAY_SECOND {$$=DateTimeExtractConstants::$EXTRACT_DAY_SECOND;} |
    DAY_MINUTE {$$=DateTimeExtractConstants::$EXTRACT_DAY_MINUTE;} |
    DAY_HOUR {$$=DateTimeExtractConstants::$EXTRACT_DAY_HOUR;} |
    YEAR_MONTH {$$=DateTimeExtractConstants::$EXTRACT_YEAR_MONTH;}
    .